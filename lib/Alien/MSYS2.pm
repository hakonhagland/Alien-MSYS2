package Alien::MSYS2;

use strict;
use warnings;
use Path::Tiny ();
use Env qw( @PATH );
use base qw( Alien::Base Exporter );

our @EXPORT    = qw( msys msys_run );
our @EXPORT_OK = qw( msys msys_run msys_path );

# ABSTRACT: Tools required for GNU style configure scripts on Windows
# VERSION

=head1 SYNOPSIS

from Perl:

 use Alien::MSYS2;
 # runs uname from MSYS2
 my $uname = msys { `uname` };

From Prompt/Makefile

 C:\> perl -MAlien::MSYS2 -e Alien::MSYS2::msys_run uname

=head1 DESCRIPTION

The difference between this module and L<Alien::MSYS> is that this module
will install MSYS2 (which includes a newer version of MSYS than the one installed
by L<Alien::MSYS>) if it is not already installed.

To clarify, MSYS is a minimal shell and POSIX tools environment for Windows.
The MSYS environment in MSYS2 contains the unix-like/cygwin based tools. It lives
under C</usr> and is special in that it is always active. All the other MSYS2 environments
( MINGW64 | MINGW32 | UCRT64 | CLANG32 | CLANG64 | CLANGARM64) inherit from the MSYS
environment and add various things on top of it.
See L<https://www.msys2.org/docs/environments/> for more information.

The MSYS environment in L<Alien::MSYS> on the other hand, was originally developed
as part of the MinGW (Minimalist GNU for Windows) project to provide a minimal POSIX
environment for building native Windows applications. Development of this MSYS has
largely ceased, and it is considered outdated compared to modern alternatives. For example,
it does not support long path names.

MSYS2 provides minimal shell and POSIX tools on Windows to enable GNU style configure
scripts to run (the type usually generated by C<autoconf>). This module aims to
provide an interface for using MSYS2 on Windows and act as a no-op on Unix like
operating systems which already have that capability.  If you use this module, I
recommend that you list this as a prerequisite only during MSWin32 installs.

When installing, this distribution will look for an existing C<MSYS2> using the following
methods in this order:

=over 4

=item environment variable C<ALIEN_INSTALL_TYPE> or C<ALIEN_MSYS_INSTALL_TYPE>

If set to C<share> a system install will not be attempted.  If set to C<system>
then a share install will not be attempted.

=item environment variable C<PERL_ALIEN_MSYS_BIN>

If set, this environment variable should be set to the root of C<MSYS2> installation.
For example, if you have C<MSYS2> installed on C<D:> you might use this:

 C:\> set PERL_ALIEN_MSYS_BIN=D:\msys64\usr\bin

Keep in mind that this environment variable is consulted during both install and at run-time,
so it is advisable to set this in the System Properties control panel.

=back

If C<MSYS> cannot be found using any of these methods, then it will download and install
C<MSYS> in this distribution's share directory.

=head1 FUNCTIONS

=head2 msys

 # get the uname from MSYS
 my $uname = msys { `uname` };
 
 # run with GNU make from MSYS instead of
 # dmake from Strawberry Perl
 msys { system 'make' };

This function takes a single argument, a code reference, and runs it with the correctly
set environment so that calls to the system function or the qx quote like operator will
use MSYS instead of the default environment.

=cut

sub msys (&)
{
  local $ENV{PATH} = $ENV{PATH};
  if($^O eq 'MSWin32')
  {
    unshift @PATH, msys_path();
  }
  $_[0]->();
}

=head2 msys_run

 # pass command through @ARGV
 C:\> perl -MAlien::MSYS2 -e Alien::MSYS2::msys_run uname
 
 # pass command through @_
 C:\> perl -MAlien::MSYS2 -e "Alien::MSYS2::msys_run 'make'; Alien::MSYS2::msys_run 'make install'"

This function runs a command with the MSYS environment.  It gets the command and arguments
either as passed to it, or if none are passed the the command is expected to be in
@ARGV.

If the command fails then it will L<exit|perlfunc#exit> with a non-zero error code.  This
is useful, in the second example above if either C<make> or C<make install> fails, then
the whole command will fail, also C<make install> will not be attempted unless C<make>
succeeds.

=cut

sub msys_run
{
  my $cmd = \@_;
  $cmd = \@ARGV unless @$cmd > 0;
  msys { system @$cmd };
  # child did an exit(0), or "success"
  return if $? == 0;
  # child wasn't able to exit (-1) or died with signal
  exit 2 if $? == -1 || $? & 127;
  # child exited with non zero
  exit $?;
}

=head2 msys_path

This function returns the full path to the MSYS bin directory.

=cut

sub msys_path ()
{
  my $class = 'Alien::MSYS2';

  return if $^O ne 'MSWin32';

  if($class->install_type('share'))
  {
    return Path::Tiny->new($class->dist_dir)->child('msys/1.0/bin')->canonpath;
  }
  else
  {
    return Path::Tiny->new($class->runtime_prop->{my_bin})->canonpath;
  }
}

1;

=head1 CAVEATS

This L<Alien> is big and slow to install.  I am aware this is an annoying problem.
It is also the only reliable way (that I know of) to install packages from source
that use autotools on Strawberry or Visual C++ Perl.  Here are some things that you
can do to avoid this painful cost:

=over 4

=item Use the system library if possible

The L<Alien::Build> system is usually smart enough to find the system library
if it is available.  L<Alien::MSYS2> is usually only necessary for so called
C<share> installs.

=item Pre-install MSYS2

As mentioned above if you preinstall MSYS2 and set the C<PERL_ALIEN_MSYS_BIN>
environment variable, then you will save yourself some time if you use multiple
installs of Perl like I do.

=item Use another build system

Some projects will provide a makefile that will work with GNU Make and C<cmd.exe>
that you can build without MSYS.  An example of an Alien that takes advantage of
this is L<Alien::libuv>.

Some projects provide both autoconf and CMake.  Although using CMake reliably
requires L<Alien::cmake3> for C<share> installs, it is much much lighter than L<Alien::MSYS2>.

Also obviously you can open a ticket, or make a pull request with the project that you
are alienizing to support build systems that don't suck as much as autoconf.

=item Use MSYS2

Strawberry Perl is convenient for building XS modules without any dependencies
or just dependencies on the small number of libraries that come bundled with
Strawberry Perl.  It is very very painful in my opinion if you depend on libraries
that are not bundled, which is why this Alien exists.  There is an alternative though.

MSYS2 / MinGW provides a MSWin32 Perl as part of a Linux / open source like package
that provides probably all of the libraries that you might want to use as dependencies,
and if it doesn't you can build much easier than with Strawberry + Alien::MSYS2.

There are some notes here:

L<https://project-renard.github.io/doc/development/meeting-log/posts/2016/05/03/windows-build-with-msys2/>

On using the MSYS2 / MinGW / MSWin32 Perl from the MSYS2 project.

=back

=cut
